//%attributes = {"lang":"en","invisible":true} comment added and reserved by 4D.
// --[HEADERSTART]-------------------------------------
// User name (OS): jfletcher
// Date and time: 04/09/12, 18:21:09
// ----------------------------------------------------
// Description:
//   Wrapper for delay process.  The main additional
//   feature is that this method attempts to detect if
//   the database is quitting.  It does this by
//   checking to see if the process was acutally
//   delayed, or not.
//
//   When 4D is quitting, calls to DELAY PROCESS do
//   nothing, so the delay should be 0.
//
//   There is some risk that the process could
//   inadvertantly exit.  If a really small delay is
//   used, for example.  I'm optimistic this won't be
//   an issue.
//
//   Of course I could just add an On Exit method to
//   the component, to be called by the user, but I
//   want this component to be zero impact on the host
//   database if possible.
// --[HEADEREND]---------------------------------------
C_LONGINT($1;$delay_l)// Optional, default is 1 second.

C_LONGINT($start_l;$end_l;$total_l)

If (Count parameters>0)
$delay_l:=$1
Else 
$delay_l:=60
End if 

$start_l:=Tickcount
DELAY PROCESS(Current process;$delay_l)
$end_l:=Tickcount

$total_l:=$end_l-$start_l

// This may be too close of a comparison.  If the total is 0 might be better.
// But I worry about it being maybe 1 instead of 0.  In all my testing $total_l
// is always less than $delay_l, though I'm not sure if 4D guarantees this.
// Keep in mind this is a development-time only component, so there is zero
// risk to production/compiled systems.
If ($total_l<$delay_l)
// Delay process is no longer working, most likely the database is quitting.
<>VC_StopMonitor_f:=True
End if 
