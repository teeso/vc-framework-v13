//%attributes = {"lang":"en","invisible":true,"executedOnServer":true} comment added and reserved by 4D.
// --[HEADERSTART]-------------------------------------
// User name (OS): Joshua Fletcher
// Date and time: 04/20/12, 13:35:53
// ----------------------------------------------------
// Description:
//   Remove any paths from the log table and delete the
//   log file.
// --[HEADEREND]---------------------------------------
C_LONGINT($0;$error_l)

C_TEXT(<>VC_EXT_LOGFILE_T)

// Delete the log file.
If (<>VC_EXT_LOGFILE_T#"")
If (Test path name(<>VC_EXT_LogFile_t)=Is a document )
DELETE DOCUMENT(<>VC_EXT_LogFile_t)
End if 
End if 

// Delete all log entries.

// Open external database.
$error_l:=VC_EXT_Open 

If ($error_l=0)

UTIL_ERR_HandlerInstall 
Begin SQL
DELETE FROM VC_BadPathLog;
End SQL
$error_l:=UTIL_ERR_HandlerRemove 

// Close external database.
If ($error_l=0)
$error_l:=VC_EXT_Close 
Else 
UTIL_ERR_ErrorMethod_t:=Current method name
// Still close the external database if another problem
// occurred but don't overwrite the error code.
VC_EXT_CloseIfError 
End if 

Else 
UTIL_ERR_ErrorMethod_t:=Current method name
End if 

$0:=$error_l
