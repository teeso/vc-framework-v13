//%attributes = {"lang":"en","invisible":true} comment added and reserved by 4D.
// --[HEADERSTART]-------------------------------------
// User name (OS): Joshua Fletcher
// Date and time: 04/20/12, 17:00:48
// ----------------------------------------------------
// Description:
//   Check to see if the method passed in still exists
//   in the database.
//
//   Note: can be optimized for performance, if you
//   pass true it will use cached/saved values instead
//   of calling METHOD GET PATHS every time.  Do this
//   for tight loops but, otherwise, you should
//   probably always get the latest list.
// --[HEADEREND]---------------------------------------
C_TEXT($1;$methodPath_t)
C_BOOLEAN($2;$useCache_f)

C_BOOLEAN($0;$methodExists_f)

C_LONGINT($zeroStamp_l;$error_l)

If (Count parameters>1)
$useCache_f:=$2
Else 
$useCache_f:=False
End if 

$methodPath_t:=$1

// If the user doesn't want to re-use cached values, get the latest paths.
If ($useCache_f=False)
$zeroStamp_l:=0
ARRAY TEXT(currentPaths_at;0)

UTIL_ERR_HandlerInstall ("VC_ERR_HandleMGP")
METHOD GET PATHS(Path All objects ;currentPaths_at;$zeroStamp_l;*)
$error_l:=UTIL_ERR_HandlerRemove 

End if 

If ($error_l=0)

If (Find in array(currentPaths_at;$methodPath_t)>0)
$methodExists_f:=True
Else 
$methodExists_f:=False
End if 

Else 
$methodExists_f:=False
End if 

$0:=$methodExists_f
