//%attributes = {"lang":"en","invisible":true} comment added and reserved by 4D.
// --[HEADERSTART]-------------------------------------
// User name (OS): jfletcher
// Date and time: 04/05/12, 15:32:06
// ----------------------------------------------------
// Description:
//   Stored procedure that monitors method changes and
//   kicks of a save accordingly.
//
//   Handles new and changed methods; see VC_DELETE_@
//   for deleted and renamed methods.
//
//   This process is launched via macro events, no
//   setup code is required in the Host.
//
//   Note: This process does all error handling (errors
//   bubble up from the rest of the code).
// --[HEADEREND]---------------------------------------
C_LONGINT(<>VC_LastMethodStamp_l)

C_BOOLEAN(VC_ProgInstalled_f)

C_LONGINT($error_l)
C_TEXT($lastMethodStamp_t)
C_BOOLEAN($showError_f)

// Set up error handling module.
UTIL_ERR_Startup 

// Set up progress bar component.
VC_ProgInstalled_f:=UTIL_ProgStartup 

// Used for DELAY PROCESS
<>VC_ExportDelay_l:=VC_CONFIG_ExportDelayGet 

// Used to determine if delete code should run from this process.
<>VC_DeleteOnChange_f:=VC_CONFIG_DeleteOnChangeGet 

// Error messages are only displayed the first time; the
// process will keep running even if there's an error in
// case the user fixes it.
$showError_f:=True

// I load this preference once, outside the loop, so
// that I don't need to connect to the external database
// for every iteration.  The only time I
// connect to the external database is if the stamp changes.
$error_l:=VC_EXT_PrefGet (<>VC_StampLabel_t;->$lastMethodStamp_t)

If ($error_l=0)

<>VC_LastMethodStamp_l:=Num($lastMethodStamp_t)

Repeat 

// ===================================================================================
// Perform tasks

If (<>VC_ERROR_F=False)
// Reset flag to show errors in case it was off.
$showError_f:=True
$error_l:=VC_EXPORT_HandleExport 
End if 


// ===================================================================================
// Perform maintenance

VC_UTIL_ProcessMaintenance ($error_l;->$showError_f;-><>VC_StopExport_f;<>VC_ExportDelay_l)

Until (<>VC_StopExport_f | <>VC_Quit_f)

Else 
UTIL_ERR_HandleError ($error_l;<>VC_ComponentName_t;Current method name)
End if 
