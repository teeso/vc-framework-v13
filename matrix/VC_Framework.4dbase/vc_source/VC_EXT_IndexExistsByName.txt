//%attributes = {"lang":"en","invisible":true} comment added and reserved by 4D.
// --[HEADERSTART]-------------------------------------
// User name (OS): Joshua Fletcher
// Date and time: 04/18/14, 17:57:37
// ----------------------------------------------------
// Description:
//   Check to see if index exists in VC_Data database.
//
// --[HEADEREND]---------------------------------------
C_TEXT($1;$indexName_t)

C_BOOLEAN($0;$indexExists_f)

C_LONGINT($found_l;$error_l)

$indexName_t:=$1

$error_l:=VC_EXT_Open 

Begin SQL
SELECT COUNT( INDEX_ID )
FROM _USER_IND_COLUMNS
WHERE INDEX_NAME LIKE :$indexName_t
INTO :$found_l;
End SQL

If ($found_l#0)
$indexExists_f:=True
End if 


If ($error_l=0)
// Close external database.
$error_l:=VC_EXT_Close 
Else 
UTIL_ERR_ErrorMethod_t:=Current method name
// Still close the external database if another problem
// occurred but don't overwrite the error code.
VC_EXT_CloseIfError 
End if 

$0:=$indexExists_f
