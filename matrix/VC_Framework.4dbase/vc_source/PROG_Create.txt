//%attributes = {"lang":"en","invisible":true} comment added and reserved by 4D.
// --[HEADERSTART]-------------------------------------
// User name (OS): Joshua Fletcher
// Date and time: 09/17/12, 17:09:33
// ----------------------------------------------------
// Description:
//   To some extent this is a wrapper for Progress New
//   but the primary motiviation is to allow a progress
//   bar to be created without showing it on screen and,
//   thus, not show it at all if the job is too fast.
//
//   The third parameter, "threshold" determines if a
//   progress bar will be displayed. If the job is
//   estimated to take longer than this value the
//   progress bar will be shown. If not, it won't.
//
//   Since a 4D Progress bar may not be opened, I
//   need to track progress-related data (title, etc.).
//   Additionally this mirror's 4D's support for
//   more than one progress bar in the same process.
//   Process Arrays are used for this.
//
//   The second parameter is used to dislay "x of y"
//   via Progress SET MESSAGE.
//
//   One enhancement to consider is window position, I always
//   open these "out of the way"...this makes this
//   progress module less-generic.
// --[HEADEREND]---------------------------------------
C_TEXT($1;$title_t)// Required, corresponds to the value used in Progress SET TITLE
C_LONGINT($2;$totalItems_l)// Optional, explained above.
C_LONGINT($3;$threshold_l)// Optional, used with Progress SET MESSAGE
C_LONGINT($4;$currentItem_l)// Optional, used with Progress SET MESSAGE
C_REAL($5;$progress_r)// Optional, used for Progress SET PROGRESS
C_TEXT($6;$message_t)// Optional, used with Progress SET MESSAGE

C_LONGINT($0;$newProgressID_l)// PROG_ id for the new progress bar; note this is NOT the 4D id

C_BOOLEAN(PROG_Displayed_f)// Flag to see if the progress window is currently open.

C_LONGINT($totalParams_l;$4DProgressBarID_l)

$title_t:=$1

$threshold_l:=0
$currentItem_l:=0
$totalItems_l:=0
$progress_r:=0
$message_t:=""

$totalParams_l:=Count parameters
If ($totalParams_l>=2)
$totalItems_l:=$2
If ($totalParams_l>=3)
$threshold_l:=$3
If ($totalParams_l>=4)
$currentItem_l:=$4
If ($totalParams_l>=5)
$progress_r:=$5
If ($totalParams_l>=6)
$message_t:=$6
End if 
End if 
End if 
End if 
End if 

PROG_Startup 

// Add new progress bar...
$newProgressID_l:=Size of array(PROG_IDs_al)+1// ID also happens to be position in the arrays.

APPEND TO ARRAY(PROG_IDs_al;$newProgressID_l)

APPEND TO ARRAY(PROG_Title_at;$title_t)

APPEND TO ARRAY(PROG_Threshold_al;$threshold_l)
APPEND TO ARRAY(PROG_Current_al;$currentItem_l)
APPEND TO ARRAY(PROG_Total_al;$totalItems_l)
APPEND TO ARRAY(PROG_Progress_ar;$progress_r)
APPEND TO ARRAY(PROG_Message_at;$message_t)

APPEND TO ARRAY(PROG_StartTime_al;0)

If ($threshold_l>0)
// Don't open the progress bar yet.
APPEND TO ARRAY(PROG_4DIDs_al;0)
Else 
$4DProgressBarID_l:=Progress New 
APPEND TO ARRAY(PROG_4DIDs_al;$4DProgressBarID_l)
Progress SET TITLE ($4DProgressBarID_l;$title_t)

If (PROG_Displayed_f=False)
// This is the first time the progress window has appeared in this process, let's position it.
// Move it to the lower-right to make slightly less obtrusive. 
Progress SET WINDOW VISIBLE (True;(Screen width-450);(Screen height-250))
PROG_Displayed_f:=True
End if 
End if 

$0:=$newProgressID_l
