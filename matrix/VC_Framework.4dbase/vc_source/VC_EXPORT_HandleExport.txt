//%attributes = {"lang":"en","invisible":true} comment added and reserved by 4D.
// --[HEADERSTART]-------------------------------------
// User name (OS): jfletcher
// Date and time: 04/05/12, 16:14:59
// ----------------------------------------------------
// Description:
//   Wrapper method called by VC_MON_ExportProcess.
//
//   I moved this to a separate method (from VC_MON_ExportProcess)
//   so that changes can be made at runtime without
//   restarting the monitor.
// --[HEADEREND]---------------------------------------
C_LONGINT($0;$error_l)

C_LONGINT($currentMethodStamp_l)

ARRAY TEXT($methodsToSave_at;0)

// Get the last method stamp.
$currentMethodStamp_l:=<>VC_LastMethodStamp_l

// Get changed methods since last stamp.
UTIL_ERR_HandlerInstall ("VC_ERR_HandleMGP")
METHOD GET PATHS(Path All objects ;$methodsToSave_at;$currentMethodStamp_l;*)
$error_l:=UTIL_ERR_HandlerRemove 

If ($error_l=0)

If (Size of array($methodsToSave_at)>0)

// ==============================================================================
// Validate method paths (invalid paths will be removed).
$error_l:=VC_METHOD_ValidateAll (->$methodsToSave_at)

If ($error_l=<>ERR_METH_BadPathsFound_l)
// This error is ignorable. The user will be warned, but
// it shouldn't stop the method export.
$error_l:=0
End if 


// ==============================================================================
// Determine create/update actions.
If ($error_l=0)
$error_l:=VC_METHOD_FindCreateUpdate (->$methodsToSave_at)
End if 

End if 

End if 


// Only run this if something actually changed. This is to minimize
// the impact of VC_METHOD_FindDelete, which can freeze design mode.
// Note there's a lot of ways the stamp can change (compiling,
// switching from User to Design, etc.) so this current test may
// still have too high of an impact. The reason I'm checking the
// stamp is I verified that the stamp will change when a method is
// deleted.
// If the performance impact is still to high, here's an alternative test.
// If( ($error_l=0)&(Size of array($methodsToSave_at)>0 )
If (($error_l=0) & (<>VC_LastMethodStamp_l#$currentMethodStamp_l))
If (<>VC_DeleteOnChange_f)
$error_l:=VC_METHOD_FindDelete 
End if 
End if 


// ==============================================================================
// Process actions.
If ($error_l=0)
$error_l:=VC_METHOD_HandleEvents 
End if 


If (VC_ProgInstalled_f)
PROG_CloseAll 
End if 


// Check to see if the stamp changed and save it if so.
If ($error_l=0)
If (<>VC_LastMethodStamp_l#$currentMethodStamp_l)
$error_l:=VC_EXT_PrefSet (<>VC_StampLabel_t;String($currentMethodStamp_l))
If ($error_l=0)
// Only update the stamp if the save was successful.
<>VC_LastMethodStamp_l:=$currentMethodStamp_l
End if 
End if 
End if 

$0:=$error_l
