//%attributes = {"lang":"en","invisible":true} comment added and reserved by 4D.
// --[HEADERSTART]-------------------------------------
// User name (OS): jfletcher
// Date and time: 04/05/12, 15:32:06
// ----------------------------------------------------
// Description:
//   Stored procedure that monitors method deletes.
//   Note that renames are treated as a delete.
//
//   This process is launched via macro events, no
//   setup code is required in the Host.
//
//   Note: This process does all error handling (errors
//   bubble up from the rest of the code).
// --[HEADEREND]---------------------------------------
C_LONGINT($error_l)
C_BOOLEAN($showError_f)

C_BOOLEAN(VC_DELETE_IgnoreOnce_f)

// Set up error handling module.
UTIL_ERR_Startup 

// METHOD GET PATHS can be slow in larger dbs.
// This threshold determines when to warn the user about it.
<>VC_MGP_Threshold_l:=VC_CONFIG_MGPThresholdGet 
//  This flag tells us if the user decided to ignore slow MGP.
<>VC_DeleteIgnore_f:=VC_CONFIG_IgnoreSlowDeleteGet 

// Used for DELAY PROCESS
<>VC_DeleteDelay_l:=VC_CONFIG_DeleteDelayGet 


// Error messages are only displayed the first time; the
// process will keep running even if there's an error in
// case the user fixes it.
$showError_f:=True


Repeat 

// ===================================================================================
// Perform tasks

If (<>VC_ERROR_F=False)
// Reset flag to show errors in case it was off.
$showError_f:=True
$error_l:=VC_DELETE_HandleDelete 
End if 

If ($error_l=0)
If (VC_DELETE_PerfTestNecessary )
// Check to see how fast it was.
$error_l:=VC_DELETE_PerfTest 
End if 
End if 


// ===================================================================================
// Perform maintenance

VC_UTIL_ProcessMaintenance ($error_l;->$showError_f;-><>VC_StopDelete_f;<>VC_DeleteDelay_l)

Until (<>VC_StopDelete_f | <>VC_Quit_f)
